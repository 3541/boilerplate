function(target_add_warning_c target warning public)
  include(CheckCCompilerFlag)

  if (MSVC)
    set(flag "/W${warning}")
  else()
    set(flag "-W${warning}")
  endif()

  check_c_compiler_flag("${flag}" "C_HAS_WARNING_${warning}")
  if (C_HAS_WARNING_${warning})
    if (${public})
      target_compile_options(${target} PUBLIC $<$<COMPILE_LANGUAGE:C>:${flag}>)
    else()
      target_compile_options(${target} PRIVATE $<$<COMPILE_LANGUAGE:C>:${flag}>)
    endif()
  endif()
endfunction()

function(target_add_warning_cxx target warning public)
  include(CheckCXXCompilerFlag)

  if (MSVC)
    set(flag "/W${warning}")
  else()
    set(flag "-W${warning}")
  endif()

  check_cxx_compiler_flag("${flag}" "CXX_HAS_WARNING_${warning}")
  if (CXX_HAS_WARNING_${warning})
    if (${public})
      target_compile_options(${target} PUBLIC $<$<COMPILE_LANGUAGE:CXX>:${flag}>)
    else()
      target_compile_options(${target} PRIVATE $<$<COMPILE_LANGUAGE:CXX>:${flag}>)
    endif()
  endif()
endfunction()

function(target_add_warning target warning public)
  get_property(languages GLOBAL PROPERTY ENABLED_LANGUAGES)
  if ("C" IN_LIST languages)
    target_add_warning_c(${target} "${warning}" ${public})
  endif()
  if ("CXX" IN_LIST languages)
    target_add_warning_cxx(${target} "${warning}" ${public})
  endif()
endfunction()

function(target_add_warnings_c target warnings public)
  foreach(warning ${warnings})
    target_add_warning_c(${target} "${warning}" ${public})
  endforeach()
endfunction()

function(target_add_warnings_cxx target warnings public)
  foreach(warning ${warnings})
    target_add_warning_cxx(${target} "${warning}" ${public})
  endforeach()
endfunction()

function(target_add_warnings target warnings public)
  foreach(warning ${warnings})
    target_add_warning(${target} "${warning}" ${public})
  endforeach()
endfunction()

function(target_add_extra_warnings target)
  if (MSVC)
    set(extra_warnings_common "")
    set(extra_warnings_c "")
    set(extra_warnings_cxx "")
  else()
    set(extra_warnings_common "disabled-optimization" "duplicated-branches" "duplicated-cond" "float-equal" "format-nonliteral" "format-security" "inline" "logical-op" "missing-declarations" "missing-include-dirs" "null-dereference" "packed" "shadow" "stack-protector" "undef" "cast-align" "cast-qual" "conversion")
    set(extra_warnings_c "bad-function-cast" "implicit" "missing-prototpyes" "nested-externs" "strict-prototypes")
    set(extra_warnings_cxx "ctor-dtor-privacy" "delete-non-virtual-dtor" "effc++")
  endif()

  target_add_warnings(${target} "${extra_warnings_common}" FALSE)

  get_property(languages GLOBAL PROPERTY ENABLED_LANGUAGES)
  if ("C" IN_LIST languages)
    target_add_warnings_c(${target} "${extra_warnings_c}" FALSE)
  endif()

  if ("CXX" IN_LIST languages)
    target_add_warnings_cxx(${target} "${extra_warnings_cxx}" FALSE)
  endif()
endfunction()
